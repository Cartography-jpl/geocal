procedure help=*
refgbl $echo
parm image1 string
parm image2 string
parm dem string
parm output1 string
parm output2 string
parm nprocess int default=24
parm grid_spacing int default=10
parm resolution real default=0.5
parm dbfile string default="scratch.db"
body
let $echo="yes"
! Remove old data. Ignore any errors here, ok for data to be missing
ush rm &dbfile &output1 &output2 || exit 0

! Create DEM in database
ush shelve_dem &dem &dbfile:dem

! Create initial image ground connection collection, fitting for 
! a constant shift in x y direction
ush shelve_igccol --rpc-line-fit=0 --rpc-sample-fit=0 &dbfile:igc_initial +
    &dbfile:dem &image1 'Image 1' &image2 'Image 2'

! Collect tiepoints for the 2 image.
ush tp_collect --number-process=&nprocess &dbfile:igc_initial &dbfile:tpcol

! Run SBA, includiing doing a height block adjustment to the DEM
ush sba --dem-height-block-adjust &dbfile:igc_initial &dbfile:tpcol +
    &dbfile:igc_sba &dbfile:tpcol_sba

! First image, reprojected to desired resolution
ush igc_project --grid-spacing=&grid_spacing +
    --resolution=&resolution &dbfile:igc_sba 0 &dbfile:surface_1
ush write_image --number-process=&nprocess &dbfile:surface_1 &output1

! Second image, reprojected on a grid matching the first image
ush igc_project --grid-spacing=&grid_spacing +
     --map-info=&dbfile:surface_1 &dbfile:igc_sba 1 &dbfile:surface_2
ush write_image --number-process=&nprocess &dbfile:surface_2 &output2

end-proc
.TITLE
pair_ortho - Correct the RPCs and then orthorectify 2 images
.HELP
PURPOSE
  This is the first step in change detection algorithm. This proc
  stitches together some python routines that corrects the RPCs if
  2 VICAR images to they are more closely coregistered. It then does
  a block height offset correction to the supplied DEM (to correct for
  gross errors in the DEM), and then it produces orthorectified data
  of the 2 images.

CALL
  pair_ortho image1 image2 dem

.LEVEL1
.VARI image1
The first image, should be in VICAR format with RPC. Can convert from
NITF using gdal_translate
.VARI image2
The second image, should be in VICAR format with RPC. Can convert from
NITF using gdal_translate
.VARI dem
Digital Elevation Model. This should be in meters from WGS 84 ellipsoid,
and can be any format that GDAL can read (e.g., VICAR)
.VARI output1
Name of file for reprojected image 1
.VARI output2
Name of file for reprojected image 2
.VARI nprocess
Number of processors to use in code that can run in parallel.
.VARI grid_spacing
Spacing between locations tht we do the RPC. We interpolate in between this.
While we can easily do every point, there isn't much point is having
the spacing much finer than the undelying DEM.
.VARI resolution
Resolution in meters of the output
.VARI dbfile
Database file used internally by the python code.
.END
